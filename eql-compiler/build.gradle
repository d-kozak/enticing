plugins {
    id 'java'
    id 'antlr'
    id 'org.jetbrains.kotlin.jvm'
}

group = "cz.vutbr.knot.enticing"
version = "0.0.1-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor",
                  "-long-messages"]
}

compileKotlin.dependsOn "generateGrammarSource"

dependencies {
    implementation(project(path:":dto",configuration:'default'))

    /**
     * Kotlin lang deps
     */
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")


    /**
     * Antlr parser generator
     */
    antlr("org.antlr:antlr4:4.7.1")
    implementation("org.antlr:antlr4:4.7.2")

    /**
     * Testing & mocking
     */
    testImplementation("io.mockk:mockk:1.9")
    testCompile("org.assertj:assertj-core:3.11.1")
    testCompile("org.junit.jupiter:junit-jupiter-api:5.3.2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.3.2")
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}