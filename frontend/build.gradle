plugins{
    id 'base'
    id "com.moowork.node" version "1.3.1"
}


group = "cz.vutbr.knot.enticing"
version = "0.0.1-SNAPSHOT"

node {
    version = '12.14.1'
    npmVersion = '6.9.0'
    download = false // use installed node (CI fix)
}

npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir 'build'
}

task test{
    dependsOn npm_run_test
}

task packageApp(type: Zip) {
    dependsOn npm_run_build
    baseName 'cz.vutbr.fit.knot.enticing.frontend'
    extension 'jar'
    destinationDir file("${projectDir}/jar")
    from('build') {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        into 'static'
    }
}

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageApp.archivePath) {
        builtBy packageApp
        type "jar"
    }
}

clean {
    delete packageApp.archivePath
}
