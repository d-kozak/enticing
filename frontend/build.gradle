buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: 'base'
apply plugin: 'com.moowork.node'

group = "cz.vutbr.knot.enticing"
version = "0.0.1-SNAPSHOT"

node {
    version = '12.3.1'
    npmVersion = '6.9.0'
    download = true
}

npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir 'build'
}

task test{
    dependsOn npm_run_test
}

task packageApp(type: Zip) {
    dependsOn npm_run_build
    baseName 'cz.vutbr.fit.knot.enticing.frontend'
    extension 'jar'
    destinationDir file("${projectDir}/jar")
    from('build') {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        into 'static'
    }
}

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageApp.archivePath) {
        builtBy packageApp
        type "jar"
    }
}

clean {
    delete packageApp.archivePath
}
